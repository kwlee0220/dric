apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
	}
}

group = 'etri'
version = '20.02.12'

sourceCompatibility = JavaVersion.VERSION_1_8;
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
	maven {
		url "http://www.datanucleus.org/downloads/maven2/"
	}
	maven {
		url "http://download.osgeo.org/webdav/geotools/"
	}
    mavenCentral()
}

sourceSets.main.java.srcDirs += 'src/main/antlr'
sourceSets.main.java.srcDirs += 'src/proto/java'
sourceSets.main.java.srcDirs += 'src/unchecked/java'
sourceSets.main.java.srcDirs += 'src/ghdfs/java'

ext.hadoop_version = '2.7.3'
ext.geotools_version = '17.2'
ext.postgresql_version = '42.0.0'
ext.postgis_jdbc_version = '2.1.0'
ext.commons_lang3_version = '3.7'
ext.commons_csv_version = '1.5'
ext.univocity_version = '2.8.1'
ext.antlr4_version = '4.7'
ext.mokito_version = '2.23.0'
ext.hamcrest_version = '1.3'
ext.kafka_2_11_version = '1.0.0'
ext.zkclient_version = '0.11'

dependencies {
	compile project(':utils')
	compile project(':marmot.common')
	compile project(':marmot.client')
}

shadowJar {
	classifier 'all'
	
	mergeServiceFiles()
	
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

javadoc {
	exclude "**/proto/**"
	options.charSet = 'UTF-8'
}
task javadocJar(type: Jar) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

ext.distDir = "${System.env.HOME}/etc/marmot/marmot.server.dist"
task distributeBinary(type:Copy) {
	dependsOn assemble
	into "${distDir}"
	from ("${projectDir}") {
		include 'bin/**'
		include 'hadoop-conf.tar'
		include '*.properties'
		include 'import_all'
		include 'misc/*'
		include '.gitignore'
		include '.project'
	}
}

artifacts {
	archives sourceJar
	archives javadocJar
	archives shadowJar
}

publishing {
	publications {
		marmot(MavenPublication) {
			from components.java
			
			artifact sourceJar
			artifact javadocJar
			artifact shadowJar
		}
	}
}

apply plugin: 'eclipse'
eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						entry.output = 'classes'
					}
				}
			}
		}
	}
}
